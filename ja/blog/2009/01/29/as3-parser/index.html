<!doctype html>
<html lang="ja">


<head>
	<meta http-equiv="content-type" content="text/html; charset=utf-8" />
	<title>10分で書いたパーサーを3時間かけて機能を増やした - てっく煮ブログ</title>
	<!--[if lt IE 9]>
	<script src="http://html5shiv.googlecode.com/svn/trunk/html5.js"></script>
	<![endif]-->
	<link rel="stylesheet" href="/stylesheets/screen.css" type="text/css" />
	<link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
	<meta property="og:title" content="10分で書いたパーサーを3時間かけて機能を増やした"/>
	<meta property="og:url" content="http://tech.nitoyon.com/ja/blog/2009/01/29/as3-parser/"/>
	<meta property="og:type" content="article"/>
	<meta property="og:description" content="昨日つくった 10分で書ける、お手軽パーサーを AS3 で が予想外に簡単だったので、練習用に機能を増やしていった。ECMA-262 3rd Edition の 11 式 (Expressions) の仕様を参考に、主要な演算子をサポートしてみた。10分で書いたやつのノリでどんどん実装していっただけ。目新しいことは何もない。代入演算子（=, += など）以外は実装できたはず。代入演算子はバックトラックが発生するので、現状の実装では対応できない。例によって、値を変更するとその場で計算してくれる。ここまで作って、条件演算子（A ? B : C）を処理するときに、A が true でも fal..."/>
	<meta property="og:image" content="http://tech.nitoyon.com/apple-touch-icon-114x114.png"/>
	<meta property="og:site_name" content="てっく煮ブログ"/>
	<meta property="fb:admins" content="1297551349" />
	<meta name="twitter:card" content="summary">
	<meta name="twitter:site" content="nitoyon">
	<meta name="viewport" content="width=device-width,initial-scale=1.0,minimum-scale=1.0,maximum-scale=1.0,user-scalable=no">
	<link rel="alternate" type="application/rss+xml" title="RSS" href="/ja/blog/index.xml" />
</head>
<body class="ja">
<header class="wrap page-header">
<div class="wrapped">
	<h1 id="logo">
		
		<a href="/ja/blog/" id="logo-blog-ja">てっく煮ブログ</a>
		
	</h1>
	<nav>
		<ul>
<li class="home"><a href="/ja/">HOME</a></li><li class="about"><a href="/ja/about/">ABOUT</a></li><li class="blog"><a href="/ja/blog/">BLOG(ja)</a></li><li class="blog-en"><a href="/en/blog/">BLOG(en)</a></li>
		</ul>
	</nav>
</div>
</header>
<div class="wrap">
<div class="wrapped">

<article>
<header>
<div class="post-date">2009年01月29日</div>

<div class="post-tags"><a href="/ja/blog/tags/actionscript/">ActionScript</a></div>

<h1>10分で書いたパーサーを3時間かけて機能を増やした</h1>


<div class="share">
	<a href="http://b.hatena.ne.jp/entry/tech.nitoyon.com/entry/20090129/as3_parser" class="hatena-bookmark-button" data-hatena-bookmark-title="10分で書いたパーサーを3時間かけて機能を増やした - てっく煮ブログ"  data-hatena-bookmark-layout="standard" title="このエントリーをはてなブックマークに追加"><img src="http://b.st-hatena.com/images/entry-button/button-only.gif" alt="このエントリーをはてなブックマークに追加" width="20" height="20" style="border: none;" /></a>
	<a href="https://twitter.com/share" class="twitter-share-button" data-url="http://tech.nitoyon.com/ja/blog/2009/01/29/as3-parser/" data-lang="ja" data-size="horizontal" data-text="10分で書いたパーサーを3時間かけて機能を増やした - てっく煮ブログ" data-related="nitoyon" data-dnt="true">Tweet</a>
	<div class="fb-like" data-href="http://tech.nitoyon.com/ja/blog/2009/01/29/as3-parser/" data-send="false" data-layout="button_count" data-width="90" data-show-faces="false"></div>
</div>

</header>

<div id="entry">
<div class="entry-body">

<p>昨日つくった <a href="/ja/blog/2009/01/28/as3-simple-parser/">10分で書ける、お手軽パーサーを AS3 で</a> が予想外に簡単だったので、練習用に機能を増やしていった。</p>
<p>ECMA-262 3rd Edition の <a href="http://www2u.biglobe.ne.jp/~oz-07ams/prog/ecma262r3/11_Expressions.html">11 式 (Expressions)</a> の仕様を参考に、主要な演算子をサポートしてみた。10分で書いたやつのノリでどんどん実装していっただけ。目新しいことは何もない。</p>
<p>代入演算子（=, += など）以外は実装できたはず。代入演算子はバックトラックが発生するので、現状の実装では対応できない。</p>
<p>例によって、値を変更するとその場で計算してくれる。</p>
<p><center><iframe src="/misc/embed_flash.html?url=%2Fmisc%2Fswf%2FComplexRecursiveDescentParsing.swf&amp;bgcolor=%23ffffff" width="400" height="200" class="flash"></iframe></center></p>
<p>ここまで作って、条件演算子（A ? B : C）を処理するときに、A が true でも false でも B と C を評価してしまうことに気づいた。いまのところ、B も C も副作用はないから問題ないんだけど、なんかかっこ悪い。</p>
<p>if 文なんかも実装していくとしたらこのままでは無理だ。パースするときに構文木を作るだけにして、その後に構文木を元に評価していくようにしなきゃいけない。なるほど、世間一般のパーサーの実装に近づいていく。</p>
<p>ソースコードは以下に(334行)。</p>
<pre class="javascript">// Complex Recursive Descent Parsing
// see also: http://fxp.hp.infoseek.co.jp/arti/parser.html
// 
package{
import flash.display.*;
import flash.events.Event;
import flash.text.*;

[SWF(backgroundColor=&quot;#ffffff&quot;)]
public class ComplexRecursiveDescentParsing extends Sprite{
    public function ComplexRecursiveDescentParsing(){
        var parser:Parser = new Parser();

        var output:TextField = new TextField();
        output.x = 5; output.y = 40;
        output.autoSize = &quot;left&quot;;
        addChild(output);

        var input:TextField = new TextField();
        input.border = true;
        input.x = input.y = 5;
        input.width = 190; input.height = 20;
        input.type = &quot;input&quot;;
        input.text = &quot;1 * 4 &gt; 2 &amp;&amp; 3 % 2 == 1 ? 100 / 3 : 3&quot;;
        input.addEventListener(&quot;change&quot;, function(event:*):void{
            try{
                output.text = parser.parse(input.text);
                output.textColor = 0x000000;
            }catch(e:Error){
                output.text = e.toString();
                output.textColor = 0xff0000;
            }
        });
        input.dispatchEvent(new Event(&quot;change&quot;));
        addChild(input);

        scaleX = scaleY = 2;
        stage.scaleMode = &quot;noScale&quot;;
        stage.align = &quot;TL&quot;;
    }
}
}

class Parser{
    private var pos:int;
    private var str:String;
    private var m:Array;

    private function Is(fn:Function):Boolean{
        var old_pos:int = pos;
        fn();
        if(pos != old_pos){
            pos = old_pos;
            return true;
        }
        return false;
    }

    public function parse(s:String):String{
        str = s.replace(/ /g, &quot;&quot;);
        trace(str);
        pos = 0;
        return ExpressionStatement().toString();
    }

    private function ExpressionStatement():Object{
        return Expression();
    }

    // Expression = AssignmentExpression { , AssignmentExpression }
    private function Expression():Object{
        var ret:Object = AssignmentExpression();
        while(str.charAt(pos) == &quot;,&quot;){
            pos++;
            ret = AssignmentExpression();
        }
        return ret;
    }

    // TODO: assignment
    // AssignmentExpression = ConditionalExpression 
    private function AssignmentExpression():Object{
        return ConditionalExpression();
    }

    // ConditionalExpression = LogicalORExpression
    //    | LogicalORExpression ? AssignmentExpression : AssignmentExpression
    private function ConditionalExpression():Object{
        var ret:Object = LogicalORExpression();

        if(str.charAt(pos) == &quot;?&quot;){
            var b:Boolean = Boolean(ret);
            var tmp:Object;
            pos++;
            tmp = AssignmentExpression();
            if(b) ret = tmp;

            if(str.charAt(pos) != &quot;:&quot;) throw new Error(&quot;Conditional Operator: no :&quot;);
            pos++;
            tmp = AssignmentExpression();
            if(!b) ret = tmp;
        }

        return ret;
    }

    // LogicalORExpression = LogicalANDExpression { || LogicalANDExpression }
    private function LogicalORExpression():Object{
        var ret:Object = LogicalANDExpression();
        while(str.substr(pos).match(/^\|\|/)){
            pos += 2;
            ret ||= LogicalANDExpression();
        }
        return ret;
    }

    // LogicalANDExpression = BitwiseORExpression { &amp;&amp; BitwiseORExpression }
    private function LogicalANDExpression():Object{
        var ret:Object = BitwiseORExpression();
        while(str.substr(pos, 2) == &quot;&amp;&amp;&quot;){
            pos += 2;
            ret &amp;&amp;= BitwiseORExpression();
        }
        return ret;
    }

    // BitwiseORExpression = BitwiseANDExpression { | BitwiseANDExpression }
    private function BitwiseORExpression():Object{
        var ret:Object = BitwiseXORExpression();
        while(str.substr(pos).match(/^\|[^|]/)){
            pos += 1;
            ret = int(ret) | int(BitwiseXORExpression());
        }
        return ret;
    }

    // BitwiseXORExpression = BitwiseANDExpression { ^ BitwiseANDExpression }
    private function BitwiseXORExpression():Object{
        var ret:Object = BitwiseANDExpression();
        while(str.substr(pos).match(/^\^/)){
            pos += 1;
            ret = int(ret) ^ int(BitwiseANDExpression());
        }
        return ret;
    }

    // BitwiseANDExpression = EqualityExpression { &amp; EqualityExpression }
    private function BitwiseANDExpression():Object{
        var ret:Object = EqualityExpression();
        while(str.substr(pos).match(/^&amp;[^&amp;]/)){
            pos += 1;
            ret = int(ret) &amp; int(EqualityExpression());
        }
        return ret;
    }

    // EqualityExpression = RelationalExpression { (==|!=|===|!==) RelationalExpression }
    private function EqualityExpression():Object{
        var ret:Object = RelationalExpression();

        while((m = str.substr(pos).match(/^(===|!==|==|!=)/))){
            switch(m[1]){
                case &quot;==&quot;:  pos += 2; ret = ret ==  RelationalExpression(); break;
                case &quot;!=&quot;:  pos += 2; ret = ret !=  RelationalExpression(); break;
                case &quot;===&quot;: pos += 3; ret = ret === RelationalExpression(); break;
                case &quot;!==&quot;: pos += 3; ret = ret !== RelationalExpression(); break;
            }
        }

        return ret;
    }

    // RelationalExpression = ShiftExpression { (&lt;|&gt;|&lt;=|&gt;=) ShiftExpression }
    private function RelationalExpression():Object{
        var ret:Object = ShiftExpression();

        while((m = str.substr(pos).match(/^(&lt;|&gt;|&lt;=|&gt;=)/))){
            switch(m[1]){
                case &quot;&lt;&quot;:  pos += 1; ret = ret &lt;  ShiftExpression(); break;
                case &quot;&gt;&quot;:  pos += 1; ret = ret &gt;  ShiftExpression(); break;
                case &quot;&lt;=&quot;: pos += 2; ret = ret &lt;= ShiftExpression(); break;
                case &quot;&gt;=&quot;: pos += 2; ret = ret &gt;= ShiftExpression(); break;
            }
        }

        return ret;
    }

    // ShiftExpression = AdditiveExpression { (&lt;&lt;|&gt;&gt;|&gt;&gt;&gt;) AdditiveExpression }
    private function ShiftExpression():Object{
        var ret:Object = AdditiveExpression();

        while((m = str.substr(pos).match(/^(&lt;&lt;|&gt;&gt;|&gt;&gt;&gt;)/))){
            switch(m[1]){
                case &quot;&lt;&lt;&quot;:  pos += 2; ret = int(ret) &lt;&lt; uint(AdditiveExpression()); break;
                case &quot;&gt;&gt;&quot;:  pos += 2; ret = int(ret) &gt;&gt; uint(AdditiveExpression()); break;
                case &quot;&gt;&gt;&gt;&quot;: pos += 3; ret = uint(ret) &gt;&gt;&gt; uint(AdditiveExpression()); break;
            }
        }

        return ret;
    }

    // AdditiveExpression = MultiplicativeExpression { (+|-) MultiplicativeExpression}
    private function AdditiveExpression():Object{
        var ret:Object = MultiplicativeExpression();

        while((m = str.substr(pos).match(/^(\+|-)/))){
            switch(m[1]){
                case &quot;+&quot;: pos++; ret = Number(ret) + Number(MultiplicativeExpression()); break;
                case &quot;-&quot;: pos++; ret = Number(ret) - Number(MultiplicativeExpression()); break;
            }
        }

        return ret;
    }

    // MultiplicativeExpression = UnaryExpression { (*|/|%) UnaryExpression}
    private function MultiplicativeExpression():Object{
        var ret:Object = UnaryExpression();

        while((m = str.substr(pos).match(/^(\*|\/|%)/))){
            switch(m[1]){
                case &quot;*&quot;: pos++; ret = Number(ret) * Number(UnaryExpression()); break;
                case &quot;/&quot;: pos++; ret = Number(ret) / Number(UnaryExpression()); break;
                case &quot;%&quot;: pos++; ret = Number(ret) % Number(UnaryExpression()); break;
            }
        }

        return ret;
    }

    // UnaryExpression = (delete|void|typeof|++|--|+|-|~|!) UnaryExpression 
    //   | PostfixExpression
    private function UnaryExpression():Object{
        if((m = str.substr(pos).match(/^(delete|void|typeof|\+\+|--|\+|-|~|!)/))){
            switch(m[1]){
                case &quot;delete&quot;: throw new Error(&quot;not implemented: delete&quot;);
                case &quot;void&quot;:   UnaryExpression(); return undefined;
                case &quot;typeof&quot;: pos += 6; return typeof UnaryExpression();
                case &quot;++&quot;:     throw new Error(&quot;not implemented: ++&quot;);
                case &quot;--&quot;:     throw new Error(&quot;not implemented: --&quot;);
                case &quot;+&quot;:      pos += 1; return Number(UnaryExpression());
                case &quot;-&quot;:      pos += 1; return -Number(UnaryExpression());
                case &quot;~&quot;:      pos += 1; return ~Number(UnaryExpression());
                case &quot;!&quot;:      pos += 1; return !Number(UnaryExpression());
            }
        }
        return PostfixExpression();
    }

    // TODO: Skipped some expressions
    // PostfixExpression = PrimaryExpression 
    private function PostfixExpression():Object{
        return PrimaryExpression();
    }

    // PrimaryExpression = this | Identifier | Literal | ArrayLiteral | ObjectLiteral
    //   | ( Expression )
    private function PrimaryExpression():Object{
        var ret:Object;
        var s:String = str.substr(pos);
        if(s.substr(0, 4) == &quot;this&quot;){
            throw new Error(&quot;not implemented: this&quot;);
        }
        else if(s.charAt(0) == &quot;(&quot;){
            pos++;
            ret = ExpressionStatement();
            if(str.charAt(pos) != &quot;)&quot;) throw new Error(&quot;No match for )&quot;);
            pos++;
            return ret;
        }
        else if(s.charAt(0) == &quot;[&quot;){
            throw new Error(&quot;not implemented: [ ]&quot;);
        }
        else if(s.charAt(0) == &quot;{&quot;){
            throw new Error(&quot;not implemented: { }&quot;);
        }
        else if(Is(Literal)){
            return Literal();
        }

        throw new Error(&quot;invalid format&quot;);
    }

    // Literal :: 
    //   NullLiteral 
    //   BooleanLiteral 
    //   NumericLiteral 
    //   StringLiteral 
    private function Literal():Object{
        if(Is(NullLiteral)){
            return NullLiteral();
        }
        if(Is(BooleanLiteral)){
            return BooleanLiteral();
        }
        if(Is(NumericLiteral)){
            return NumericLiteral();
        }
        return null;
    }

    // NullLiteral = null
    private function NullLiteral():Object{
        if(str.substr(pos, 4) == &quot;null&quot;){
            pos += 4;
            return null;
        }
        return undefined;
    }

    // BooleanLiteral = true | false
    private function BooleanLiteral():Object{
        if(str.substr(pos, 4) == &quot;true&quot;){
            pos += 4;
            return true;
        }else if(str.substr(pos, 5) == &quot;false&quot;){
            pos += 5;
            return false;
        }
        return undefined;
    }

    // TODO: HexDigit and ExponentialPart are not supported
    // NumericLiteral
    private function NumericLiteral():Object{
        if((m = str.substr(pos).match(/^([\d\.]+)/))){
            pos += m[1].length;
            return parseFloat(m[1]);
        }
        return undefined;
    }
}</pre>

</div>

<footer>

<div id="post-ad">
<script async src="//pagead2.googlesyndication.com/pagead/js/adsbygoogle.js"></script>
<ins class="adsbygoogle my_adslot_1"
     style="display:inline-block"
     data-ad-client="ca-pub-5030829344952718"
     data-ad-slot="4154719447"></ins>
<script>
(adsbygoogle = window.adsbygoogle || []).push({});
</script>
<ins class="adsbygoogle my_adslot_2"
     style="display:inline-block"
     data-ad-client="ca-pub-5030829344952718"
     data-ad-slot="5631452649"></ins>
<script>
(adsbygoogle = window.adsbygoogle || []).push({});
</script>

</div>

<div class="share">

<div class="share">
	<a href="http://b.hatena.ne.jp/entry/tech.nitoyon.com/entry/20090129/as3_parser" class="hatena-bookmark-button" data-hatena-bookmark-title="10分で書いたパーサーを3時間かけて機能を増やした - てっく煮ブログ"  data-hatena-bookmark-layout="standard" title="このエントリーをはてなブックマークに追加"><img src="http://b.st-hatena.com/images/entry-button/button-only.gif" alt="このエントリーをはてなブックマークに追加" width="20" height="20" style="border: none;" /></a>
	<a href="https://twitter.com/share" class="twitter-share-button" data-url="http://tech.nitoyon.com/ja/blog/2009/01/29/as3-parser/" data-lang="ja" data-size="horizontal" data-text="10分で書いたパーサーを3時間かけて機能を増やした - てっく煮ブログ" data-related="nitoyon" data-dnt="true">Tweet</a>
	<div class="fb-like" data-href="http://tech.nitoyon.com/ja/blog/2009/01/29/as3-parser/" data-send="false" data-layout="button_count" data-width="90" data-show-faces="false"></div>
</div>

</div>

<div class="entry-data">
<div class="post-date">2009年01月29日 <span class="edit-history"><a href="https://github.com/nitoyon/tech.nitoyon.com/commits/master/_posts/ja/2009-01-29-as3-parser.htn">編集履歴</a></span></div>

<div class="post-tags"><a href="/ja/blog/tags/actionscript/">ActionScript</a></div>
</div>



<div id="pager">
	
	<div class="previous">
		<a href="/ja/blog/2009/01/28/as3-simple-parser/" rel="previous"><b>&laquo;</b><span class="title">10分で書ける、お手軽パーサーを AS3 で</span></a>
	</div>
	
	
	<div class="next">
		<a href="/ja/blog/2009/01/30/as3-parser-node/" rel="next"><span class="title">パーサーが生成する構文木を木にしてみた</span><b>&raquo;</b></a>
	</div>
	
</div>

<div id="disqus_thread"></div>
<noscript>Please enable JavaScript to view the <a href="http://disqus.com/?ref_noscript">comments powered by Disqus.</a></noscript>
<a href="http://disqus.com" class="dsq-brlink">comments powered by <span class="logo-disqus">Disqus</span></a>

</footer>
</div><!--entry-->

</article>

</div>
</div>

<footer class="page-footer">
<h2>About</h2>
<div class="wrapped">
<div id="footer-right-column" class="column">
	<div id="profile">
		<h3>Profile</h3>

		<a href="/about.html" id="prof_pic"><img alt="Photo" src="/img/icon/pic.jpg"></a>
		<p>nitoyon (にとよん)</p>
		<p>京都のベンチャー会社勤務。プログラマ、たまに趣味でデザイン。</p>
		<p class="seemore"><a href="/ja/about/">詳細</a></p>
	</div>

	
	<div id="subscribe">
		<h3>Subscribe</h3>
		<ul>
			<li><a href="https://twitter.com/nitoyon" class="twitter-icon32">twitter: @nitoyon</a></li>
			<li><a href="https://www.facebook.com/TechNiBlogJa" class="facebook-icon32">Facebook ページ</a></li>
			<li><a href="/ja/blog/index.xml" class="rss-icon32">RSS</a></li>
		</ul>
	</div>
</div>

<div id="footer-left-column" class="column">
	<div id="recententries">
		<h3>Recent Entries</h3>
		<ul id="recententries_list">
			<li><img src="/images/loading.gif"><noscript>(JavaScript required)</noscript></li>
		</ul>
	</div>
</div>
</div><!--wrapped-->

<div class="wrapped">
	<div id="archive">
	<h3>Archive</h3>
	<ul id="archive_month_list">
		<li><img src="/images/loading.gif"><noscript>(JavaScript required)</noscript></li>
	</ul>
	</div>
</div><!-- wrapped -->

<div class="wrap copy">
&copy; nitoyon 2002-2014. All rights reserved.
</div>
</footer>


<script src="http://ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js"></script>
<script src="/ja/posts.js"></script>
<script src="/javascripts/techni.js"></script>
<div id="fb-root"></div>
</body>
</html>